def checkoutRepository(def repoDir) {
    checkout(
            [$class           : 'GitSCM',
             branches         : [[name: ghprbTargetBranch]],
             extensions       : [
                     [$class: 'RelativeTargetDirectory', relativeTargetDir: repoDir],
                     [$class: 'LocalBranch', localBranch: ghprbTargetBranch]],
             userRemoteConfigs: [[credentialsId: 'github-service-account', url: ghprbAuthorRepoGitUrl]]])

}

def mergePRLocally(def repoDir) {
    dir(repoDir) {
        sh "git checkout ${ghprbSourceBranch}"
        sh "git checkout ${ghprbTargetBranch}"
        sh "git merge ${ghprbSourceBranch} --no-ff"
    }
}

def buildApplication(def repoDir) {
    dir(repoDir) {
        sh "mvn package"
    }
}

def pushChangesToRemote(def repoDir) {
    dir(repoDir) {
        withCredentials([string(credentialsId: 'github-access-token', variable: 'token')]) {
            repoURL = "https://${token}@github.com/${ghprbGhRepository}"
            sh "git remote set-url origin ${repoURL}"
            sh "git push origin ${ghprbTargetBranch}"
        }
    }
}

def buildAndPushDockerImage(def repoDir, def registryURL, def imageNamespace, def imageTag) {
    dir(repoDir) {
        sh "docker build -t ${registryURL}/${imageNamespace}:${imageTag} ."
    }
    withCredentials([usernamePassword(credentialsId: 'acr_credentials', passwordVariable: 'acr_password', usernameVariable: 'acr_username')]) {
        sh "docker login ${registryURL} --username ${acr_username} --password ${acr_password} "
    }
    dir(repoDir) {
        sh "docker push ${registryURL}/${imageNamespace}:${imageTag}"
    }
}

def handleError(def err) {
    echo "${err}"
    echo "${err.toString()}"
    echo "${err.getMessage()}"
    err.printStackTrace()
    postPRComment("Deployment failed with error message: ${err.getMessage()}.")
    currentBuild.result = "FAILURE"
}

def pushImageAsLatest(def registryURL, def imageNamespace, def imageTag) {
    sh "docker tag ${registryURL}/${imageNamespace}:${imageTag} ${registryURL}/${imageNamespace}:latest"
    sh "docker push ${registryURL}/${imageNamespace}:latest"
}

def checkMergeStateStatus() {
    def repoOwner = ghprbGhRepository.split('/')[0]
    def repoName = ghprbGhRepository.split('/')[1]
    def body = /{"query":"query{repository(owner:\"${repoOwner}\",name:\"${repoName}\"){pullRequest(number: ${ghprbPullId}){mergeStateStatus}}}"}/
    writeFile file: 'body.json', text: body
    sh 'cat body.json'
    withCredentials([string(credentialsId: 'github-access-token', variable: 'token')]) {
       def output = sh returnStdout: true, script: "curl -H 'Authorization: bearer ${token}' -H 'Accept: application/vnd.github.merge-info-preview+json'-X POST https://api.github.com/graphql -d @body.json"
       echo output
       writeFile file: 'output.json', text: output
      def stateStatus = sh returnStdout: true, script: "jq -r '.data.repository.pullRequest.mergeStateStatus' output.json"
      echo stateStatus
      if (!stateStatus.equals("CLEAR") && !stateStatus.equals("UNSTABLE")) {
        error("Deployment cannot be completed, because PR is not in mergeable state")
      }
    }
}

def postPRComment(def commentBody) {
    def requestBody = /{"body":"${commentBody}"}/
    writeFile file: 'body.json', text: requestBody
    withCredentials([string(credentialsId: 'github-access-token', variable: 'token')]) {
        def output = sh returnStdout: true, script: "curl -X POST -H 'Authorization: token ${token}' https://api.github.com/repos/${ghprbGhRepository}/issues/${ghprbPullId}/comments -d @body.json"
        echo output
    }
}


node() {
    def repoDir = 'application_repository'
    def registryURL = 'pwacr.azurecr.io'
    def imageNamespace = 'java-hello-world'
    def imageTag = "${ghprbTargetBranch}_${ghprbPullId}"

    try {
        //w pierwszej kolejności skrypt sprawdza, czy PR jest w odpowiednim statusie
        // zasady "mergowalności" są określone w repozytorium, więc wszelkie obliczenia dokonuje GitHub i zwraca tylko status poprzez API
        checkMergeStateStatus()

        //dalej skrypt klonuje repozytorium na gałęzi docelowej
        checkoutRepository(repoDir)

        //do gałęzi docelowej jest wykonywana operacja "git merge"
        //w ten sposób skrypt lokalnie odwzorowuje wersje aplikacji, która zostanie uzyskana w wyniku akceptacji PR
        //dzięki temu docelowo na serwerze znajdzie się dokładnie taka aplikacja, jaka na gałęzi main
        mergePRLocally(repoDir)

        //aplikacja jest budowana przy pomocy Mavena, zgodnie z procesem określonym w pom.xml
        buildApplication(repoDir)

        //budowany jest obraz Docker, zgodnie z procesem określonym w pliku Dockerfile
        //obraz budowany jest z unikalnym tagiem zbudowanym z nazwy gałęzi docelowej oraz numeru PR
        //dzięki temu obraz jest unikalny i łatwo wyszukiwalny w rejestrze ACR
        buildAndPushDockerImage(repoDir, registryURL, imageNamespace, imageTag)

        //po pomyślnym zbudowaniu aplikacji skrypt wykonuje "git push" do GitHuba
        //GitHub wykrywa operację automatycznie i akceptuje PR - nie jest konieczna dodatkowa operacja
        pushChangesToRemote(repoDir)

        //jeśli gałąź docelowa to main, to do zbudowanego wcześniej obrazu jest dodawany również tag latest
        //dzięki temu aplikacja w Azure automatycznie będzie pobierała najnowszą wersję z gałęzi "main"
        if (ghprbTargetBranch.equals('main')) {
            pushImageAsLatest(registryURL, imageNamespace, imageTag)
        }
    } catch (err) {
        //metoda obsługująca błędy (systemowe bądź wytworzone metodą 'error()'
        //wyświetli informacje o błędzie w logach oraz w komentarzu w GitHubie
        handleError(err)

    } finally {
        //metoda wbudowana, czyści workspace Jenkinsa
        //dzięki temu każde wykonanie otrzymuje nowe, czyste środowisko działania
        cleanWs()
    }

}